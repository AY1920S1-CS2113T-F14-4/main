plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'org.openjfx.javafxplugin' version '0.0.7'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.4'
}

checkstyle {
    toolVersion = '8.23'
}

shadowJar {
    archiveBaseName = "full"
    archiveVersion = "v1.3"
    archiveClassifier = null
    archiveAppendix = null
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task coverage(type: JacocoReport) {
    sourceDirectories.from files(sourceSets.main.allSource.srcDirs)
    classDirectories.from files(sourceSets.main.output)
    executionData.from files(jacocoTestReport.executionData)
    afterEvaluate {
        classDirectories.from files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/*.jar'])
        })
    }
    reports {
        html.enabled = true
        xml.enabled = true
    }
}

coveralls {
    sourceDirs = sourceSets.main.allSource.srcDirs.absolutePath
    jacocoReportPath = "${buildDir}/reports/jacoco/coverage/coverage.xml"
}

tasks.coveralls {
    dependsOn coverage
}

group 'seedu.spinbox'
version '1.1.0'

repositories {
    mavenCentral()
}

javafx {
    version = "11.0.2"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

application {
    // Change this to your main class.
    mainClassName = "spinbox.gui.Launcher"
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.0'
    compile group: 'com.joestelmach', name: 'natty', version: '0.6'
}

test {
    useJUnitPlatform()
}

run {
    standardInput = System.in
}